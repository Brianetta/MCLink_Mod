evaluationDependsOnChildren()

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

version = "0.1.0"

allprojects {
    group = "net.dries007.mclink"
    archivesBaseName = "MCLink"
    apply plugin: 'maven'
    sourceCompatibility = targetCompatibility = "1.7"
    compileJava {
        sourceCompatibility = targetCompatibility = "1.7"
    }
}

repositories {
    mavenCentral()
}
    
dependencies {
    compile 'com.google.guava:guava:17.0'
    compile 'com.google.code.gson:gson:2.2.4'
}

task setupDecompWorkspace
task setupDevWorkspace
task setupCiWorkspace

subprojects {

    version = rootProject.version

    dependencies {
        compile rootProject
    }

    sourceSets.main.resources.srcDirs += rootProject.sourceSets.main.resources.srcDirs

    jar {
        duplicatesStrategy = 'exclude'
        from rootProject.sourceSets.main.output
    }

    if (project.hasProperty('minecraft'))
    {
        sourceJar {
            duplicatesStrategy = 'exclude'
            from rootProject.sourceSets.main.allSource
        }

        project.tasks.withType(Jar) { jarTask ->
            jarTask.appendix = project.minecraft.version
        }

        processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'
                expand 'version':project.version, 'mcversion':project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

        rootProject.setupDecompWorkspace.dependsOn setupDecompWorkspace
        rootProject.setupDevWorkspace.dependsOn setupDevWorkspace
        rootProject.setupCiWorkspace.dependsOn setupCiWorkspace
    }

    rootProject.build.dependsOn build
}
